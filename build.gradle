plugins {
    id 'groovy-gradle-plugin'
    id 'maven-publish'
    id "com.gradle.plugin-publish" version "0.12.0"
}

group = 'io.sdkman'
archivesBaseName = 'sdkman-vendor-gradle-plugin'

repositories {
    jcenter()
}

java {
    withSourcesJar()
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
}

dependencies {
    compileOnly localGroovy()
    testImplementation 'org.spockframework:spock-core:1.3-groovy-2.5'
    testImplementation 'cglib:cglib-nodep:3.1'
    testImplementation 'com.github.tomakehurst:wiremock:2.26.3'
}

def gradleVersionsToTest = ['6.1.1', '6.2.2', '6.5.1', '6.9', '7.0.2']

gradleVersionsToTest.each {version ->
    tasks.register("testGradle$version", Test) { task ->
        task.group = "Verification"
        task.description = "Tests plugin with Gradle $version"
        task.classpath = files(sourceSets.test.output, sourceSets.main.output, configurations.testRuntimeClasspath)
        task.testClassesDirs = sourceSets.test.output
        task.systemProperty('gradle.testing.version', version)
    }
}

check.dependsOn tasks.withType(Test)

gradlePlugin {
    plugins {
        sdkmanVendorPlugin {
            id = 'io.sdkman.vendors'
            implementationClass = 'io.sdkman.vendors.SdkmanVendorPlugin'
        }
    }
}

pluginBundle {
    website = 'http://sdkman.io/'
    vcsUrl = 'https://github.com/sdkman/gradle-sdkman-vendor-plugin'
    description = 'The SDKMAN Vendor Gradle Plugin.'
    tags = ['sdkman', 'sdk']

    plugins {
        sdkmanVendorPlugin {
            id = 'io.sdkman.vendors'
            displayName = 'SDKMAN! Vendor Plugin'
        }
    }
}

publishing {
    publications {
        plugin(MavenPublication) {
            from components.java

            artifact sourcesJar {
                classifier "sources"
            }

            pom.withXml {
                def root = asNode()
                root.appendNode('name', 'SDKMAN! Vendor Gradle plugin')
                root.appendNode('description', 'Gradle plugin for SDKMAN! Vendors to publish Releases.')
                root.appendNode('inceptionYear', '2018')

                def license = root.appendNode('licenses').appendNode('license')
                license.appendNode('name', 'The Apache Software License, Version 2.0')
                license.appendNode('url', 'http://www.apache.org/licenses/LICENSE-2.0.txt')
                license.appendNode('distribution', 'repo')

                def developer = root.appendNode('developers').appendNode('developer')
                developer.appendNode('name', 'Marco Vermeulen')
                developer.appendNode('email', 'marco@sdkman.io')
            }

        }
    }
}
